# Модульне тестування

Тести є чудовим способом для забезпечення стабільних, перевірених
додатків. Вони служать в якості інтерактивної документації і дозволяють
змінювати код, не побоюючись порушити функціональність. Мова D
забезпечує зручний і рідний синтаксис `unittest`, як частину
мови D. Блоки `unittest` можуть бути використані для тестування
функціональності вихідного коду будь-де у межах модулів D.

    // Блок для моєї функції
    unittest
    {
        assert(myAbs(-1) == 1);
        assert(myAbs(1)  == 1);
    }

Це уможливлює пряму [розробку через тестування](https://uk.wikipedia.org/wiki/Test-driven_development)
за необхідністю.

### Запуск і реалізація блоків `unittest`

Блоки `unittest` можуть містити довільний код, який компілюється і
запускається лише тоді, коли додано параметер `-unittest` у командному
рядку при компіляції за допомогою компілятора DMD. DUB також має
опцію компіляції і запуску тестів через команду `dub test`.

### Перевірка прикладів з `assert`

Зазвичай, `unittest`-и містять вирази `assert`, які перевіряють
функціональність потрібної функції. Блоки `unittest` зазвичай
розташовані поблизу реалізації функції, яка може бути або з самого
початку вихідного коду, або всередині класів чи структур.

### Збільшення покриття коду

Юніт тести є потужною зброєю для забезпечення куленепробивних додатків.
Загальною мірою при перевірці покриття програми за допомогою тестів
є _покриття коду_. Це співвідношення виконується всупереч існуючих
рядків коду. Компілятор DMD дозволяє легко створювати звіти
щодо покриття коду шляхом додавання опції `-cov`. Файл `.lst`, в якому
міститься детальна статистика, буде створений для кожного модуля.

Так як компілятор може вивести атрибути для шаблонного коду автоматично,
є загальний патерн для додавання анотацій у юніт тести, щоб забезпечити
атрибути для протестованого коду:

    unittest @safe @nogc nothrow pure
    {
        assert(myAbs() == 1);
    }

### Поглиблення

- [Модульне тестування у _Programming in D_](http://ddili.org/ders/d.en/unit_testing.html)
- [Модульне тестування у D](https://dlang.org/spec/unittest.html)

## {SourceCode}

```d
import std.stdio : writeln;

struct Vector3 {
    double x;
    double y;
    double z;

    double dot(Vector3 rhs) const {
        return x*rhs.x + y*rhs.y + z*rhs.z;
    }

    // Нічого страшного!
    unittest {
        assert(Vector3(1,0,0).dot(
          Vector3(0,1,0)) == 0);
    }

    string toString() const {
        import std.string : format;
        return format("x:%.1f y:%.1f z:%.1f",
          x, y, z);
    }

    // .. і це також!
    unittest {
        assert(Vector3(1,0,0).toString() ==
          "x:1.0 y:0.0 z:0.0");
    }
}

void main()
{
    Vector3 vec = Vector3(0,1,0);
    writeln(`Цей вектор протестовано: `,
      vec);
}

/*
Це не буде скомпільовано і буде
ігноруватись у звичайному режимі.
Виконайте 'dub test' локально або
скомпілюйте проект з 'dmd -unittest'
для перевірки цих модулів.
*/
unittest {
    Vector3 vec;
    // .init є спеціальним вбудованим
    // параметром, який повертає
    // початкове значення типу
    assert(vec.x == double.init);
}
```
