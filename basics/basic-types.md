# Базовi типи данних

D надає ряд основних типів, які завжди мають той же
розмір **незалежно**  від платформи. Єдиним винятком
є тип `real`, який надає максимально можливу
точність для чисел з плаваючою точкою. Немає ніякої різниці
між розміром цілого числа незалежно від того, чи була програма
скомпільована для 32-бітової чи 64-бітової системи.

<table class="table table-hover">
<tr><td width="250px"><code class="prettyprint">bool</code></td> <td>8 біт</td></tr>
<tr><td><code class="prettyprint">byte, ubyte, char</code></td> <td>8 біт</td></tr>
<tr><td><code class="prettyprint">short, ushort, wchar</code></td> <td>16 біт</td></tr>
<tr><td><code class="prettyprint">int, uint, dchar</code></td> <td>32 біта</td></tr>
<tr><td><code class="prettyprint">long, ulong</code></td> <td>64 біта</td></tr>
</table>

#### Типи з плаваючою точкою:

<table class="table table-hover">
<tr><td width="250px"><code class="prettyprint">float</code></td> <td>32 біта</td></tr>
<tr><td><code class="prettyprint">double</code></td> <td>64 біта</td></tr>
<tr><td><code class="prettyprint">real</code></td> <td>залежно вiд платформи, 80 бiт на Intel x86-32</td></tr>
</table>

Префікс `u` позначає *беззнаковi* (unsigned) типи. `char` є UTF-8
символом, `wchar` використовується в UTF-16 рядках і `dchar` в UTF-32 рядках.

Перетворення між змінними різних типів допускається компілятором лише
якщо немає втрати точностi. Хоча конверсія між плаваючими типами
(наприклад `double` у `float`) допускається.

Перетворення в інший тип може бути примусовим при використаннi виразу
`cast(<ТИП>) <змінна>`. Перетворення необхідно використовувати з великою
обережністю, оскiльки виразу `cast` дозволено порушувати систему типів.

Спеціальне ключове слово `auto` створює змінну і виводить її
тип з правої частини виразу. Наприклад, `auto MyVar = 7` виведе тип
`int` для `myVar`. Зверніть увагу, що тип буде визначено під час
компіляції і він не може бути змінений пiзнiше - так само, як з
будь-якою іншою змінною з явно заданим типом.

### Властивостi типiв

Всі типи даних мають властивість `.init` - значення, яким вони
iнiціалізуються. Для всіх цілих чисел це "0", для типiв з плаваючою
точкою це `NaN` (*not a number, Не число*).

Цілі типи та типи з плаваючою точкою мають властивість `.max`, яка
повертає найбільше значення, яке вони можуть містити. Цілі типи
також мають властивість `.min` для найменшого значення, яке вони можуть
містити, у той час, як типи з плаваючою точкою мають властивість
`.min_normal`, яка визначається найменшим представимим нормованним
значенням, не рівне 0.

Типи з плаваючою точкою також мають властивість `.NaN` (NaN-значення),
`.infinity` (значення нескінченності), `.dig` (точнiсть у кількостi
десяткових цифр),` .mant_dig` (кількість бітів у мантисі) і багато інших.

Кожен тип також має властивість `.stringof`, яка повертає його назву
у вигляді рядка.

### Iндекси у D

У D індекси мають зазвичай тип псевдоніма (Alias) `size_t`, так як це
є досить великий тип, щоб представляти зміщення в усій адресній
пам'яті - тобто `uint` для 32-розрядних і `ulong` для 64-розрядних
архітектур.

### Asserts

`assert` є вбудований у компілятор вираз, який перевіряє умови у режимі
налагодження (Debug) і завершує роботу з `AssertionError`, якщо умова
не виконується.

### Детальнiше

#### Основнi посилання

- [Присвоювання](http://ddili.org/ders/d.en/assignment.html)
- [Змiннi](http://ddili.org/ders/d.en/variables.html)
- [Арифметика](http://ddili.org/ders/d.en/arithmetic.html)
- [Плаваюча точка](http://ddili.org/ders/d.en/floating_point.html)
- [Фундаментальнi типи у _Programming in D_](http://ddili.org/ders/d.en/types.html)

#### Додаткові посилання

- [Огляд усiх базових типiв у D](https://dlang.org/spec/type.html)
- [`auto` та `typeof` у _Programming in D_](http://ddili.org/ders/d.en/auto_and_typeof.html)
- [Властивостi типiв](https://dlang.org/spec/property.html)

## {SourceCode}

```d
import std.stdio;

void main()
{
    // Великі числа можуть бути
    // вiдокремленi пiдкреслюванням  "_"
    // для полiпшення читабельності.
    int b = 7_000_000;
    // потрiбне перетворення
    short c = cast(short) b;
    uint d = b; // добре
    int g;
    assert(g == 0);

    auto f = 3.1415f; // f має тип float

    // typeid(VAR) повертає iнформацию про тип
    // виразу.
    writeln("type of f is ", typeid(f));
    double pi = f; // добре
    // для плаваючих типiв неявне
    // понижуючє приведення допускається
    float demoted = pi;

    // доступ до властивостей типiв
    assert(int.init == 0);
    assert(int.sizeof == 4);
    assert(bool.max == 1);
    writeln(int.min, " ", int.max);
    writeln(int.stringof); // int
}
```
